*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        remove_latest_checkpoint
Test Template     Remove Latest Checkpoint Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    VMName            VMHost        AdvancedOptions
Remove Latest Checkpoint with valid inputs without providing VM Host and Advanced Options
                      [Tags]    main_scenarios
                      OK        ${Source_VM}      ${EMPTY}      ${EMPTY}

Remove Latest Checkpoint with valid Inputs while providing VM Host and Advanced Options
                      OK        ${Source_VM}      ${VM_Host}    -RunAsynchronously

Assertion With Non-Existing VM Name
                      [Tags]    main_scenarios
                      NOK       Invalid           ${VM_Host}    ${EMPTY}

Assert Behaviour when more than one VM exists for provided VM Name
                      NOK       VM*               ${EMPTY}      ${EMPTY}

Assertion With Non-Existing VM Host
                      NOK       ${Source_VM}      Invalid       ${EMPTY}

Assertion for Non-Existing Advanced Parameter
                      NOK       ${Source_VM}      ${VM_Host}    Invalid

Assertion for VM Name as Mandatory Field
                      NOK       ${EMPTY}          ${VM_Host}    -RunAsynchronously

*** Keywords ***
Startup
    Log    *******Start Test "Remove Latest Checkpoint"*******
    Log    *******Connect to AE*******
    Connect AE

Remove Latest Checkpoint Template
    [Arguments]    ${OK_NOK}    ${VMName}    ${VMHost}    ${AdvancedOptions}
    [Documentation]    This test is used to remove latest checkpoint for given VM.
    ...    -Precondition: To run this test suite, SCVMM credentials should be known to user.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK/FAULT_OTHER. Value of this variable should be OK or NOK.
    ...    -${VMName}: This field specifies Name of the Virtual Machinefor which you want to remove latest checkpoint.
    ...    -${VMHost}: This field Specify Name of the VM Host in which VM is located..
    ...    -${AdvancedOptions}: This field specifies advanced parameter for remove-scvmcheckpoint command.
    Generate String
    Action Create    ${_REMOVE_LATEST_CHECKPOINT_ACTION}
    Common Inputs
    Run Keyword If    '${VMName}' == 'Invalid'    Action Set    &UC4RB_VMM_VMNAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VMM_VMNAME#    ${VMName}
    Run Keyword If    '${VMHost}' == 'Invalid'    Action Set    &UC4RB_VMM_VMHOST#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VMM_VMHOST#    ${VMHost}
    Run Keyword If    '${AdvancedOptions}' == 'Invalid'    Action Set    &UC4RB_VMM_ADV_OPTIONS#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VMM_ADV_OPTIONS#    ${AdvancedOptions}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure

Teardown
    Log    *******End Test "Remove Latest Checkpoint"*******

*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        get_vm_names
Test Template     Get VM Names Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    VMNameFilter      VMHost        AdvancedOptions
Get VM while providing VM name Filter
                      [Tags]    main_scenarios
                      OK        VM*               ${EMPTY}      ${EMPTY}

Get VM without providing any input
                      OK        ${EMPTY}          ${EMPTY}      ${EMPTY}

Get VM while providing Advanced Option only
                      OK        ${EMPTY}          ${EMPTY}      ${Advance_opt}

Get VM while providing VM Host only
                      OK        ${EMPTY}          V*            ${EMPTY}

Assertion With Non-Existing VM Name
                      [Tags]    main_scenarios
                      NOK       Invalid           ${VM_Host}    ${EMPTY}

Assertion With Non-Existing VM Host
                      NOK       ${Source_VM}      Invalid       ${EMPTY}

Assertion for Non-Existing Advanced Parameter
                      NOK       ${Source_VM}      ${VM_Host}    Invalid

*** Keywords ***
Startup
    Log    *******Start Test "Get VM Names"*******
    Log    *******Connect to AE*******
    Connect AE

Get VM Names Template
    [Arguments]    ${OK_NOK}    ${VMNameFilter}    ${VMHost}    ${AdvancedOptions}
    [Documentation]    This test is used to get VM(s)
    ...    -Precondition: To run this test suite, SCVMM credentials should be known to user.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK/FAULT_OTHER. Value of this variable should be OK or NOK.
    ...    -${VMNameFilter}: This field specifies Filter for Virtual Machine.
    ...    -${VMHost}: This field Specify the host from which you want to get VM(s_.
    ...    -${AdvancedOptions}: This field specifies advanced parameter for Get-SCVirtualMachine command.
    Generate String
    Action Create    ${_GET_VM_NAMES_ACTION}
    Common Inputs
    Run Keyword If    '${VMNameFilter}' == 'Invalid'    Action Set    &UC4RB_VMM_VMNAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VMM_VMNAME#    ${VMNameFilter}
    Run Keyword If    '${VMHost}' == 'Invalid'    Action Set    &UC4RB_VMM_VMHOST#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VMM_VMHOST#    ${VMHost}
    Run Keyword If    '${AdvancedOptions}' == 'Invalid'    Action Set    &UC4RB_VMM_ADV_OPTIONS#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VMM_ADV_OPTIONS#    ${AdvancedOptions}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Count Value Should Be    &UC4RB_VMM_VM_COUNT#
    Run Keyword If    '${OK_NOK}' == 'OK' and '&UC4RB_VMM_VM_COUNT#' > '0'    Action Object Variable Should Be Found    &UC4RB_VMM_VM_NAME#
    Run Keyword If    '${OK_NOK}' == 'OK' and '&UC4RB_VMM_VM_COUNT#' == '0'    Action Object Variable Should Not Be Found    &UC4RB_VMM_VM_NAME#
    Run Keyword If    '${OK_NOK}' == 'NOK'    Action Object Variable Should Not Be Found    &UC4RB_VMM_VM_COUNT#
    Run Keyword If    '${OK_NOK}' == 'NOK'    Action Object Variable Should Not Be Found    &UC4RB_VMM_VM_NAME#

Teardown
    Log    *******End Test "Get VM Names"*******

*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        create_checkpoint
Default Tags
Test Template     Create Checkpoint Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    VMName            VMHost        CheckpointName        Description       AdvancedOptions
Create Checkpoint With Valid Inputs Without Providing VM Host,Description and Advanced Options
                      [Tags]    main_scenarios
                      NOK       ${Source_VM}      ${EMPTY}      ${Checkpoint_Name}    ${EMPTY}          ${EMPTY}

Create Checkpoint With Valid Inputs also providing VM Host,Description and Advanced Options
                      [Tags]    main_scenarios
                      OK        ${Source_VM}      ${VM_Host}    Generated             ${Description}    -RunAsynchronously

Assertion With Non-Existing VM Name
                      [Tags]    main_scenarios
                      NOK       Invalid           ${VM_Host}    Generated             ${EMPTY}          ${EMPTY}

Assertion when more than one VM exists for the provided VM Name
                      NOK       VM*               ${EMPTY}      Generated             ${EMPTY}          ${EMPTY}

Assertion With Non-Existing Advanced Parameter
                      NOK       ${Source_VM}      ${VM_Host}    Generated             ${EMPTY}          Invalid

Assertion With Non-Existing VM Host
                      NOK       ${Source_VM}      Invalid       Generated             ${EMPTY}          ${EMPTY}

Assertion for VM Name as Mandatory Field
                      NOK       ${EMPTY}          ${VM_Host}    Generated             ${Description}    -RunAsynchronously

Assertion for Checkpoint Name as Mandatory Field
                      NOK       ${Source_VM}      ${VM_Host}    ${EMPTY}              ${Description}    -RunAsynchronously

*** Keywords ***
Startup
    Log    *******Start Test "Create Checkpoint"*******
    Log    *******Connect to AE*******
    Connect AE

Create Checkpoint Template
    [Arguments]    ${OK_NOK}    ${VMName}    ${VMHost}    ${CheckpointName}    ${Description}    ${AdvancedOptions}
    [Documentation]    This test is used to get checkpoint for the given VM.
    ...    -Precondition: To run this test suite, SCVMM credentials should be known to user.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK/FAULT_OTHER. Value of this variable should be OK or NOK.
    ...    -${VMName}: This field Specify Name of the Virtual Machine for which you want to create a checkpoint.
    ...    -${VMHost}: Specify name of the VM Host in which VM is located.
    ...    -${CheckpointName} : This field Specify the name of the Checkpoint you want to create.
    ...    -${Description}: This field Specify the Description for the checkpoint being created.
    ...    -${AdvancedOptions}: This field specifies advanced parameter for creating checkpoints for respective virtual machine command.
    Generate String
    Action Create    ${_CREATE_CHECKPOINT_ACTION}
    Common Inputs
    Run Keyword If    '${VMName}' == 'Invalid'    Action Set    &UC4RB_VMM_VMNAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VMM_VMNAME#    ${VMName}
    Run Keyword If    '${VMHost}' == 'Invalid'    Action Set    &UC4RB_VMM_VMHOST#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VMM_VMHOST#    ${VMHost}
    Run Keyword If    '${CheckpointName}' == 'Generated'    Action Set    &UC4RB_VMM_CHECKPOINT#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VMM_CHECKPOINT#    ${CheckpointName}
    Run Keyword If    '${AdvancedOptions}' == 'Invalid'    Action Set    &UC4RB_VMM_ADV_OPTIONS#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VMM_ADV_OPTIONS#    ${AdvancedOptions}
    Action Set    &UC4RB_VMM_DESC#    ${Description}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure

Teardown
    Log    *******End Test "Create Checkpoint"*******

*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        get_disk_drive
Default Tags
Test Template     Get Disk Drive Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    VM_Name           VM_Host       AdvancedOptions
Get Disks With Valid Inputs With Providing VM Name,VM Host and Advanced Options
                      [Tags]    main_scenarios
                      OK        ${Source_VM}      ${VM_Host}    -RunAsynchronously

Get Disks With Valid Inputs Without Providing VM Host & Advanced Options
                      OK        ${Source_VM}      ${EMPTY}      ${EMPTY}

Get Disks With Valid Inputs Without Providing VM Name & Advanced Options
                      OK        ${EMPTY}          ${VM_Host}    ${EMPTY}

Assertion With VM Name & VM Host As Optional Field
                      OK        ${EMPTY}          ${EMPTY}      ${EMPTY}

Assert Behaviour when more than one VM exists for provided VM Name
                      [Tags]    main_scenarios
                      NOK       VM*               ${EMPTY}      ${EMPTY}

Assertion With Non-Existing VM Name
                      NOK       Invalid           ${VM_Host}    ${EMPTY}

Assertion With Non-Existing VM Host
                      NOK       ${Source_VM}      Invalid       ${EMPTY}

Assertion for Non-Existing Advanced Parameter
                      NOK       ${Source_VM}      ${VM_Host}    Invalid

*** Keywords ***
Startup
    Log    *******Start Test "Get Disk Drive"*******
    Log    *******Connect to AE*******
    Connect AE

Get Disk Drive Template
    [Arguments]    ${OK_NOK}    ${VM_Name}    ${VM_Host}    ${AdvancedOptions}
    [Documentation]    This test is used to get the disks for given VM.
    ...    -Precondition: To run this test suite, SCVMM credentials should be known to user.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK/FAULT_OTHER. Value of this variable should be OK or NOK.
    ...    -${VM_Name}: This field specifies Name of the Virtual Machine you want to get disks.
    ...    -${VM_Host}: This field Specify the host from which you want to get disks.
    ...    -${AdvancedOptions}: This field specifies advanced parameter for get disks command.
    Generate String
    Action Create    ${_GET_DISK_DRIVE_ACTION}
    Common Inputs
    Run Keyword If    '${VM_Name}' == 'Invalid'    Action Set    &UC4RB_VMM_VMNAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VMM_VMNAME#    ${VM_Name}
    Run Keyword If    '${VM_Host}' == 'Invalid'    Action Set    &UC4RB_VMM_VMHOST#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VMM_VMHOST#    ${VM_Host}
    Run Keyword If    '${AdvancedOptions}' == 'Invalid'    Action Set    &UC4RB_VMM_ADV_OPTIONS#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VMM_ADV_OPTIONS#    ${AdvancedOptions}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure

Teardown
    Log    *******End Test "Get Disk Drive"*******

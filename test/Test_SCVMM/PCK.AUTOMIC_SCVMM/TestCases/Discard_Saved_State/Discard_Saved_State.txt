*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        discard_saved_state
Test Template     Discard Saved State Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    VMName            VMHost        AdvancedOptions
Discard Saved State with Valid Inputs without providing VM Host and Advanced Options
                      [Tags]    main_scenarios
                      OK        ${Source_VM}      ${EMPTY}      ${EMPTY}

Discard Saved State an already stopped VM also providing VM Host and Advanced Options
                      OK        ${Source_VM}      ${VM_Host}    -RunAsynchronously

Assert Behaviour for Non-Existing VM
                      [Tags]    main_scenarios
                      NOK       Invalid           ${VM_Host}    ${EMPTY}

Assert Behaviour when more than one VM exists for provided VM Name
                      NOK       VM*               ${EMPTY}      ${EMPTY}

Assert Behaviour for Non-Existing VM Host
                      NOK       ${Source_VM}      Invalid       ${EMPTY}

Assert Behaviour for Non-Existing Advanced Parameter
                      NOK       ${Source_VM}      ${VM_Host}    Invalid

Assert VM Name as Mandatory Field
                      NOK       ${EMPTY}          ${VM_Host}    -RunAsynchronously

*** Keywords ***
Startup
    Log    *******Start Test "Discard Saved State"*******
    Log    *******Connect to AE*******
    Connect AE

Discard Saved State Template
    [Arguments]    ${OK_NOK}    ${VMName}    ${VMHost}    ${AdvancedOptions}
    [Documentation]    This action is used to discard the saved state of a virtual machine deployed on hosts managed by Virtual Machine Manager (VMM).
    ...    -Precondition: To execute this test variables used should be changed accordingly.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK/FAULT_OTHER. Value of this variable should be OK or NOK.
    ...    -${VMName}: This field specify name of the VM you want to move to discard its saved state.
    ...    -${VMHost}: This field specifies the host where vm is located
    ...    -${AdvancedOptions}: This field specifies advanced parameter for discard saved state .
    Generate String
    Action Create    ${_DISCARD_SAVED_STATE_ACTION}
    Common Inputs
    Run Keyword If    '${VMName}' == 'Invalid'    Action Set    &UC4RB_VMM_VMNAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VMM_VMNAME#    ${VMName}
    Run Keyword If    '${VMHost}' == 'Invalid'    Action Set    &UC4RB_VMM_VMHOST#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VMM_VMHOST#    ${VMHost}
    Run Keyword If    '${AdvancedOptions}' == 'Invalid'    Action Set    &UC4RB_VMM_ADV_OPTIONS#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VMM_ADV_OPTIONS#    ${AdvancedOptions}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure

Teardown
    Log    *******End Test "Discard Saved State"*******

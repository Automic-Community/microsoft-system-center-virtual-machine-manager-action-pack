*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        save_vm_state
Test Template     Save VM State Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    VMName            VMHost        AdvancedOptions
Save VM State with Valid Inputs without providing VM Host and Advanced Options
                      [Tags]    main_scenarios
                      OK        ${Source_VM}      ${EMPTY}      ${EMPTY}

Saving VM State which is already in saved state also providing VM Host and Advanced Options
                      OK        ${Source_VM}      ${VM_Host}    -RunAsynchronously

Assertion when more than 1 VM exists for the provided VM name
                      NOK       VM*               ${EMPTY}      ${EMPTY}

Assert Behaviour for Non-Existing VM
                      NOK       Invalid           ${VM_Host}    ${EMPTY}

Assert Behaviour for Non-Existing VM Host
                      NOK       ${Source_VM}      Invalid       ${EMPTY}

Assert Behaviour for Non-Existing Advanced Parameter
                      NOK       ${Source_VM}      ${VM_Host}    Invalid

Assert VM Name as Mandatory Field
                      NOK       ${EMPTY}          ${VM_Host}    -RunAsynchronously

*** Keywords ***
Startup
    Log    *******Start Test "Save VM State"*******
    Log    *******Connect to AE*******
    Connect AE

Save VM State Template
    [Arguments]    ${OK_NOK}    ${VMName}    ${VMHost}    ${AdvancedOptions}
    [Documentation]    This action is used to save the state of a virtual machine deployed on hosts managed by Virtual Machine Manager (VMM).
    ...    -Precondition: To execute this test variables used should be changed accordingly.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK/FAULT_OTHER. Value of this variable should be OK or NOK.
    ...    -${VMName}: This field specify name of the VM you want to move to saved state.
    ...    -${VMHost}: This field specifies the host where vm is located
    ...    -${AdvancedOptions}: This field specifies advanced parameter for save-scvirtualmachine-state command.
    Generate String
    Action Create    ${_SAVE_VM_STATE_ACTION}
    Common Inputs
    Run Keyword If    '${VMName}' == 'Invalid'    Action Set    &UC4RB_VMM_VMNAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VMM_VMNAME#    ${VMName}
    Run Keyword If    '${VMHost}' == 'Invalid'    Action Set    &UC4RB_VMM_VMHOST#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VMM_VMHOST#    ${VMHost}
    Run Keyword If    '${AdvancedOptions}' == 'Invalid'    Action Set    &UC4RB_VMM_ADV_OPTIONS#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VMM_ADV_OPTIONS#    ${AdvancedOptions}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure

Teardown
    Log    *******End Test "Save VM State"*******

*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        common_failures
Default Tags
Test Template     Common Failures Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    ServerIP         Port           Username           Password           VMName          VMHost
Assertion with Invalid Server IP
                      NOK       Invalid          ${VMM_Port}    ${VMM_Username}    ${VMM_Password}    ${Source_VM}    ${VM_Host}

Assertion with Invalid Port
                      NOK       ${VMM_Server}    Invalid        ${VMM_Username}    ${VMM_Password}    ${Source_VM}    ${VM_Host}

Assertion with Non-Existing Username
                      NOK       ${VMM_Server}    ${VMM_Port}    Invalid            ${VMM_Password}    ${Source_VM}    ${VM_Host}

Assertion with Invalid Password
                      NOK       ${VMM_Server}    ${VMM_Port}    ${VMM_Username}    Invalid            ${Source_VM}    ${VM_Host}

Assert Server IP As a Mandatory Field
                      NOK       ${EMPTY}         ${VMM_Port}    ${VMM_Username}    ${VMM_Password}    ${Source_VM}    ${VM_Host}

Assert Port As a Mandatory Field
                      NOK       ${VMM_Server}    ${EMPTY}       ${VMM_Username}    ${VMM_Password}    ${Source_VM}    ${VM_Host}

Assert Username As a Mandatory Field
                      NOK       ${VMM_Server}    ${VMM_Port}    ${EMPTY}           ${VMM_Password}    ${Source_VM}    ${VM_Host}

Assert Password As a Mandatory Field
                      NOK       ${VMM_Server}    ${VMM_Port}    ${VMM_Username}    ${EMPTY}           ${Source_VM}    ${VM_Host}

*** Keywords ***
Startup
    Log    *******Start Test "COMMON FAILURES"*******
    Log    *******Connect to AE*******
    Connect AE

Common Failures Template
    [Arguments]    ${OK_NOK}    ${ServerIP}    ${Port}    ${Username}    ${Password}    ${VMName}
    ...    ${VMHost}
    [Documentation]    This test executes all the commom negative test case scenarios of SCVMM Package Actions.
    ...    -Precondition: To run this test suite SCVMM credentials should be known to user.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK, NOK or OTHER.
    ...    -${Server_IP}: This field specifies Specify IP address or DNS name of the SCVMM server to which you want to connect.
    ...    -${Port}: This field specifies port which will be used for connection.
    ...    -${Username}: This field specifies username which will be used for this connection.
    ...    -${Password}: This field specifies password for the given username.
    ...    -${VMName}: This field specifies name of the virtual machine you want to get
    ...    -${VMHost}: This field specifies the host from which you want to get VM.
    Generate String
    Action Create    ${_GET_VM_ACTION}
    Connect to SCVMM
    Action Set    &UC4RB_VMM_VMNAME#    ${VMName}
    Action Set    &UC4RB_VMM_VMHOST#    ${VMHost}
    Set Overwrite Agent
    Action Execute
    Assert Failure

Teardown
    Log    *******End Test "COMMON FAILURES"*******

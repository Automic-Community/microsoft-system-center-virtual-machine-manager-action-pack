*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        move_vm
Default Tags
Test Template     Move VM Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    VMName            SourceVMHost         TargetVMHost    DestinationPath    MoveOptions                                     AdvancedOptions
Move VM With Valid Inputs With Providing Sorce VM Host,MoveOptions as Discard saved state and Advanced Options
                      [Tags]    main_scenarios
                      OK        ${VM_TO_MOVE}     ${Source_VM_Host}    ${VM_Host}      [${VM_Path}]       Discard saved state                             -RunAsynchronously

Assertion With Non-Existing VM Name
                      [Tags]    main_scenarios
                      NOK       Invalid           ${VM_Host}           ${VM_Host}      [${VM_Path}]       ${EMPTY}                                        ${EMPTY}

Assertion when more than 1 VM exists for the provided VM name
                      NOK       VM*               ${EMPTY}             ${VM_Host}      [${VM_Path}]       ${EMPTY}                                        ${EMPTY}

Assertion With Non-Existing Source VM Host
                      NOK       ${VM_TO_MOVE}     Invalid              ${VM_Host}      [${VM_Path}]       Discard saved state                             ${EMPTY}

Assertion With Non-Existing Target VM Host
                      NOK       ${VM_TO_MOVE}     ${VM_Host}           Invalid         [${VM_Path}]       Discard saved state                             ${EMPTY}

Assertion With Non-Existing Destination Path
                      NOK       ${VM_TO_MOVE}     ${VM_Host}           ${VM_Host}      Invalid            Start VM after successful completion of move    ${EMPTY}

Assertion for Non-Existing Advanced Parameter
                      NOK       ${VM_TO_MOVE}     ${VM_Host}           ${VM_Host}      [${VM_Path}]       Discard saved state                             Invalid

Assert VM Name as Mandatory Field
                      NOK       ${EMPTY}          ${VM_Host}           ${VM_Host}      [${VM_Path}]       Discard saved state                             -RunAsynchronously

Assert Target VM Host as Mandatory Field
                      NOK       ${VM_TO_MOVE}     ${VM_Host}           ${EMPTY}        [${VM_Path}]       Start VM after successful completion of move    -RunAsynchronously

Assert Destination Path as Mandatory Field
                      NOK       ${VM_TO_MOVE}     ${VM_Host}           ${VM_Host}      ${EMPTY}           Start VM after successful completion of move    -RunAsynchronously

*** Keywords ***
Startup
    Log    *******Start Test "Move VM"*******
    Log    *******Connect to AE*******
    Connect AE

Move VM Template
    [Arguments]    ${OK_NOK}    ${VMName}    ${SourceVMHost}    ${TargetVMHost}    ${DestinationPath}    ${MoveOptions}
    ...    ${AdvancedOptions}
    [Documentation]    This test is used to move the given VM to the provided host/path.
    ...    -Precondition: To run this test suite, SCVMM credentials should be known to user.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK/FAULT_OTHER. Value of this variable should be OK or NOK.
    ...    -${VMName}: This field specifies Name of the Virtual Machine you want to move to the provided host.
    ...    -${SourceVMHost}:This field specify the host on which VM is located.
    ...    -${TargetVMHost}: This field specify the host to which VM will be moved.
    ...    -${DestinationPath }: This field specify destination path to move VM.
    ...    -${MoveOptions} : This field specify whether to Discard saved state of VM or Start VM after successful completion of move.
    ...    -${AdvancedOptions}: This field specifies advanced parameter for move-scvirtualmachine command.
    Generate String
    Action Create    ${_MOVE_VM_ACTION}
    Common Inputs
    Run Keyword If    '${VMName}' == 'Invalid'    Action Set    &UC4RB_VMM_VMNAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VMM_VMNAME#    ${VMName}
    Run Keyword If    '${SourceVMHost}' == 'Invalid'    Action Set    &UC4RB_VMM_VMHOST#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VMM_VMHOST#    ${SourceVMHost}
    Run Keyword If    '${TargetVMHost}' == 'Invalid'    Action Set    &UC4RB_VMM_DEST_VMHOST#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VMM_DEST_VMHOST#    ${TargetVMHost}
    Run Keyword If    '${DestinationPath}' == 'Invalid'    Action Set    &UC4RB_VMM_DEST_PATH#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VMM_DEST_PATH#    ${DestinationPath}
    Run Keyword If    '${AdvancedOptions}' == 'Invalid'    Action Set    &UC4RB_VMM_ADV_OPTIONS#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_VMM_ADV_OPTIONS#    ${AdvancedOptions}
    Action Set    &UC4RB_VMM_MOVE_OPTIONS#    ${MoveOptions}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure

Teardown
    Log    *******End Test "Move VM"*******
